import { array_contains } from "std/array"
import { env_var_get } from "std/env"
import { dir_exists, file_exists, file_glob } from "std/fs"

const generated_docs = "src/std/docs"
const repo_docs = "amber-docs/docs/{trust env_var_get("LATEST_VERSION")}/stdlib/doc"

/// Echo error in the GitHub-friendly format
fun echo_error(message: Text, do_exit: Bool = true): Null {
    echo "::error::{message}"
    if do_exit: exit 1
}

for path in ["src/std/", generated_docs, repo_docs] {
    echo "Listing {path} directory..."
    trust $ ls {path} $
}

echo "Comparing documentation..."
echo "Generated docs: {generated_docs}"
echo "Repository docs: {repo_docs}"

// Check if both directories exist
for dir in [generated_docs, repo_docs] {
    if not dir_exists(dir) {
        echo_error("Generated docs directory does not exist: {dir}")
    }
}

// Get sorted list of markdown files (basenames only) in temp files
cd generated_docs
const generated_files = trust file_glob("*.md")
cd "-"
cd repo_docs
const repo_files = trust file_glob("*.md")
cd "-"

// Count files properly (handle empty case)
const generated_count = len(generated_files)
const repo_count = len(repo_files)

echo "Generated files count: {generated_count}"
echo "Repository files count: {repo_count}"

// Check if no files were found
if generated_count == 0 {
    echo_error("No markdown files found in generated docs directory")
}
if repo_count == 0 {
    echo_error("No markdown files found in repository docs directory")
}

// Use comm to find differences between file lists
let only_in_generated = [Text]
let only_in_repo = [Text]

for generated in generated_files {
    if not array_contains(repo_files, generated)  {
        only_in_generated += [generated]
    }
}
for repo in repo_files {
    if not array_contains(generated_files, repo)  {
        only_in_generated += [repo]
    }
}

if len(only_in_generated) > 0 {
    echo_error("Files in generated docs but missing in repository: {only_in_generated}")
}

if len(only_in_repo) > 0 {
    echo_error("Files in repository but missing in generated docs: {only_in_repo}")
}

// Compare content of each file (both lists are now identical)
let diff_found = false
for file in generated_files {
    if not file_exists("{generated_docs}/{file}") or not file_exists("{repo_docs}/{file}")  {
        echo_error("File not found: {file}", false)
        diff_found = true
        continue
    }
    $ diff -q "{generated_docs}/{file}" "{repo_docs}/{file}" > /dev/null 2>&1 $ failed(code) {
        echo_error("Content differs for file: {file}", false)
        echo "--- Diff for $file ---"
        trust $ diff "{generated_docs}/{file}" "{repo_docs}/{file}" || true $
        echo "--- End of diff ---"
        diff_found = true
    }
}

if diff_found {
    echo_error("Documentation content does not match")
}

echo "âœ… All documentation files match!"
